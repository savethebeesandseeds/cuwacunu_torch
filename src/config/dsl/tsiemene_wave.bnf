<instruction>            ::= {<wave>} ;

<wave>                   ::= "WAVE" [<whitespace>] <wave_name> [<whitespace>] "{"
                             <line_end>
                             {<wave_stmt>}
                             [<whitespace>] "}" <line_end> ;

<wave_stmt>              ::= [<whitespace>] <wave_stmt_core> ;
<wave_stmt_core>         ::= <mode_assignment>
                           | <epochs_assignment>
                           | <batch_size_assignment>
                           | <max_batches_per_epoch_assignment>
                           | <wikimyei_block>
                           | <source_block> ;

<mode_assignment>        ::= "MODE" [<whitespace>] "=" [<whitespace>] <mode_value> [<whitespace>] ";" <line_end> ;
<epochs_assignment>      ::= "EPOCHS" [<whitespace>] "=" [<whitespace>] <unsigned_int> [<whitespace>] ";" <line_end> ;
<batch_size_assignment>  ::= "BATCH_SIZE" [<whitespace>] "=" [<whitespace>] <unsigned_int> [<whitespace>] ";" <line_end> ;
<max_batches_per_epoch_assignment> ::= "MAX_BATCHES_PER_EPOCH" [<whitespace>] "=" [<whitespace>] <unsigned_int> [<whitespace>] ";" <line_end> ;

<wikimyei_block>         ::= "WIKIMYEI" [<whitespace>] <path_ref> [<whitespace>] "{"
                             <line_end>
                             {<wikimyei_assignment>}
                             [<whitespace>] "}" [<whitespace>] ";" <line_end> ;
<wikimyei_assignment>    ::= [<whitespace>] <wikimyei_assignment_core> ;
<wikimyei_assignment_core> ::= <wikimyei_path_assignment>
                             | <wikimyei_train_assignment>
                             | <wikimyei_profile_id_assignment> ;
<wikimyei_path_assignment>      ::= "PATH" [<whitespace>] "=" [<whitespace>] <path_value> [<whitespace>] ";" <line_end> ;
<wikimyei_train_assignment>     ::= "TRAIN" [<whitespace>] "=" [<whitespace>] <bool_value> [<whitespace>] ";" <line_end> ;
<wikimyei_profile_id_assignment> ::= "PROFILE_ID" [<whitespace>] "=" [<whitespace>] <id_value> [<whitespace>] ";" <line_end> ;

<source_block>           ::= "SOURCE" [<whitespace>] <path_ref> [<whitespace>] "{"
                             <line_end>
                             {<source_assignment>}
                             [<whitespace>] "}" [<whitespace>] ";" <line_end> ;
<source_assignment>      ::= [<whitespace>] <source_assignment_core> ;
<source_assignment_core> ::= <source_path_assignment>
                           | <source_symbol_assignment>
                           | <source_from_assignment>
                           | <source_to_assignment> ;
<source_path_assignment>   ::= "PATH" [<whitespace>] "=" [<whitespace>] <path_value> [<whitespace>] ";" <line_end> ;
<source_symbol_assignment> ::= "SYMBOL" [<whitespace>] "=" [<whitespace>] <symbol_value> [<whitespace>] ";" <line_end> ;
<source_from_assignment>   ::= "FROM" [<whitespace>] "=" [<whitespace>] <date_value> [<whitespace>] ";" <line_end> ;
<source_to_assignment>     ::= "TO" [<whitespace>] "=" [<whitespace>] <date_value> [<whitespace>] ";" <line_end> ;

<wave_name>              ::= <id_value> ;
<path_ref>               ::= <path_value> ;
<path_value>             ::= <id_value> ;
<id_value>               ::= <id_char> {<id_char>} ;
<mode_value>             ::= "train" | "run" ;
<bool_value>             ::= "true" | "false" | "1" | "0" ;
<symbol_value>           ::= <symbol_char> {<symbol_char>} ;
<date_value>             ::= <digit><digit> "." <digit><digit> "." <digit><digit><digit><digit> ;
<unsigned_int>           ::= <digit> {<digit>} ;

<id_char>                ::= <letter> | <digit> | "_" | "." | "-" ;
<symbol_char>            ::= <letter> | <digit> | "_" | "." | "-" ;

<line_end>               ::= [<whitespace>] <break_block> ;
<break_block>            ::= {<newline>} ;
<whitespace>             ::= {<empty>} ;
<letter>                 ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;
<digit>                  ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
<newline>                ::= "\n" | "\r\n" ;
<empty>                  ::= " " | "\t" ;

